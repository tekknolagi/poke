/* pkl-rt-2.pkl - Run-time library for the poke compiler.  */

/* Copyright (C) 2021, 2022 The poke authors.  */

/* This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

/* Any function that uses `format' cannot be used in `pkl-rt-1'.
   The reason why the problem is happening is that `format' is using
   `pkl_asm_call' to call `_pkl_reduce_string_array' and `pkl_asm_call'
   relies on the global environment in the compiler and at that time the
   on-going environment is not added yet.
   So we have to put functions that use `format' here, in `pkl-rt-2'.  */

/* Assertion function.

   The compiler transforms assert statement to invocation of this
   function.  COND is first argument of assert statement, and MSG is
   the optional second argument.  FILENAME is the name of source
   file.  LINE and COL are, respectively, line and column number
   of the assert statement in the source file.  */

fun _pkl_assert = (uint<64> cond, string msg, string filename,
                   uint<64> line, uint<64> col) void:
  {
    if (cond)
      return;

    raise Exception {
      code = EC_assert,
      name = "assertion failure",
      location = format ("%s:%u64d:%u64d:", filename, line, col),
      msg = msg,
      exit_status = 1,
    };
  }

/* The Type struct below describes the values returned by the `typeof'
   language construction.  This type is used in the compiler phases,
   so pretty please do not use `typeof' until the compiler has been
   bootstrapped.

   Beware: if you do changes to any of the variables or data types
   defined below then you have to adapt the code generator
   accordingly.  */

var PK_TYPE_UNKNOWN =  0;
var PK_TYPE_INTEGRAL = 1;
var PK_TYPE_OFFSET   = 2;
var PK_TYPE_STRING   = 3;
var PK_TYPE_ARRAY    = 4;
var PK_TYPE_STRUCT   = 5;
var PK_TYPE_FUNCTION = 6;

type Type =
  struct
  {
    uint<32> code;
    string name;
    union
    {
      struct
      {
        int<32> signed_p;
        uint<64> size;
      } integral : code == PK_TYPE_INTEGRAL;

      struct
      {
        int<32> signed_p;
        uint<64> size;
        uint<64> _unit;
      } offset : code == PK_TYPE_OFFSET;

      struct
      {
        int<32> bounded_p;
        /* etype */
      } array : code == PK_TYPE_ARRAY;

      struct
      {
        int<32> union_p;
        int<32> pinned_p;
        int<32> integral_p;
        int<32> isign;
        uint<64> isize;
        string[] fields;
        string[] methods;
      } sct : code == PK_TYPE_STRUCT;

      struct {} empty;
    } attrs;

    method _print = void:
    {
      print "#<type:";
      if (code == PK_TYPE_INTEGRAL)
        printf ("integral,signed_p=%i32d,size=%u64d",
                attrs.integral.signed_p, attrs.integral.size);
      else if (code == PK_TYPE_OFFSET)
        printf ("offset,signed_p=%i32d,size=%u64d,unit=%u64d",
                attrs.offset.signed_p, attrs.offset.size,
                attrs.offset._unit);
      else if (code == PK_TYPE_STRING)
        print "string";
      else if (code == PK_TYPE_ARRAY)
        printf ("array%s", attrs.array.bounded_p ? "" : ",unbounded");
      else if (code == PK_TYPE_STRUCT)
        printf ("%s%s%s",
                attrs.sct.union_p ? "union" : "struct",
                attrs.sct.pinned_p ? ",pinned" : "",
                attrs.sct.integral_p ? format (",isign=%i32d,isize=%i32d",
                                               attrs.sct.isign, attrs.sct.isize)
                                     : "");
      else
        print "unknown";
      if (name != "")
        printf ",name=%v", name;
      print ">";
    }
  };
